buildscript {
    repositories {
        gradlePluginPortal()
        mavenCentral()
        jcenter()
        maven { url 'https://maven.minecraftforge.net' }
        maven { url 'https://repo.spongepowered.org/repository/maven-public/' }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:[6.0,6.2]'
        classpath group: 'org.spongepowered', name: 'mixingradle', version: '0.7-SNAPSHOT'
    }
}

plugins {
    id 'eclipse'
    id 'maven-publish'
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'org.spongepowered.mixin'

version = mc_version + "-" + mod_version
group = maven_group
archivesBaseName = mod_id

String current_date = new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
String mvn_auth = System.getenv("MVN_AUTH")

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

repositories {
    maven { url 'https://repo.spongepowered.org/repository/maven-public/' }
}

tasks.withType(JavaCompile).configureEach {
    options.deprecation(false)
    options.warnings(false)
}

dependencies {
    minecraft "net.minecraftforge:forge:${mc_version}-${forge_version}"
}

minecraft {
    mappings channel: 'official', version: '1.20'
    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')
    runs {
        client {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            property 'forge.enabledGameTestNamespaces', 'snakerlib'
            mods {
                snakerlib { source sourceSets.main }
            }
        }
        data {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            args '--mod', 'snakerlib', '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')
            mods {
                snakerlib { source sourceSets.main }
            }
        }
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

jar.finalizedBy('reobfJar')

jar {
    manifest {
        attributes(
                [
                        "Specification-Title"     : mod_title,
                        "Specification-Vendor"    : mod_author,
                        "Specification-Version"   : mod_version,
                        "Implementation-Title"    : mod_title,
                        "Implementation-Vendor"   : mod_author,
                        "Implementation-Version"  : mod_version,
                        "Implementation-Timestamp": current_date
                ]
        )
    }
}

tasks.register('srcJar', Jar) {
    build.dependsOn it
    from sourceSets.main.allSource
    archiveClassifier = 'sources'
}

publishing {
    repositories {
        maven {
            url "https://mymavenrepo.com/repo/DuqP6QPb7DvFrT8khf7h"
            credentials {
                username System.getenv("MVN_USER")
                password System.getenv("MVN_PASS")
            }
        }
    }
    publications {
        mavenJava(MavenPublication) {
            artifact srcJar
            artifact jar
        }
    }
}

tasks.withType(JavaCompile).configureEach { options.encoding = 'UTF-8' }